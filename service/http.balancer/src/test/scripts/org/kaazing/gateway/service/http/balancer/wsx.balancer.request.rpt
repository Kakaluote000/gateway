#
# Copyright 2007-2016, Kaazing Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

property writeMask ${httpx:randomBytes(4)}
property hostHeader "gateway.kaazing.com:8001"
property connectAddress "http://localhost:8001/echo"
property redirectAddress "http://node.kaazing.com:8001/echo"

connect ${connectAddress}
connected

write http:method "GET"
write http:version "HTTP/1.1"
write http:header "Host" ${hostHeader}
write http:header "Connection" "Upgrade"
write http:header "Upgrade" "websocket"
write http:header "Sec-WebSocket-Version" "13"
write http:header "Sec-WebSocket-Key" "dGhlIHNhbXBsZSBub25jZQ=="
write http:header "Sec-WebSocket-Protocol" "x-kaazing-handshake"

read http:status "101" /.+/
read http:version "HTTP/1.1"
read http:header "Connection" "Upgrade"
read http:header "Date" /.+/
read http:header "Sec-WebSocket-Accept" "s3pPLMBiTxaQ9kYGzzhZRbK+xOo="
read http:header "Sec-WebSocket-Protocol" "x-kaazing-handshake"
read http:header "Server" /.+/
read http:header "Upgrade" "websocket"

# binary-encoded balancer directive
read [0x82 0x04 0xEF 0x83 0xBF 0x4E]

write [0x81 0xFE 0x00 0xA6] ${writeMask}
write option mask ${writeMask}
write "GET /echo HTTP/1.1\r\n"
write "WebSocket-Protocol: \r\n"
write "Sec-WebSocket-Protocol: \r\n"
write "Sec-WebSocket-Extensions: x-kaazing-idle-timeout,x-kaazing-ping-pong,x-kaazing-http-revalidate\r\n"
write "\r\n"
write option mask [0x00 0x00 0x00 0x00]

read [0x82] [0..3]
read "HTTP/1.1 302 Found\r\n"
read /Date: .+\r\n/
read "location: " ${redirectAddress} "\r\n"
read /Sec-WebSocket-Extensions: .+\r\n/
read "Server: Kaazing Gateway\r\n"
read "Content-Length: 0\r\n"
read "\r\n"
